//===================== Copyright (c) Valve Corporation. All Rights Reserved. ======================
//
// Example pixel shader that can be applied to models
//
//==================================================================================================

// STATIC: "CONVERT_TO_SRGB"			"0..0"
// STATIC: "FLASHLIGHT"					"0..1"
// STATIC: "FLASHLIGHTDEPTHFILTERMODE"	"0..2"
// STATIC: "LIGHTMAPPED"				"0..1"
// STATIC: "EMISSION"					"0..1"

// DYNAMIC: "WRITEWATERFOGTODESTALPHA"  "0..1"
// DYNAMIC: "PIXELFOGTYPE"				"0..1"
// DYNAMIC: "NUM_LIGHTS"				"0..4"
// DYNAMIC: "WRITE_DEPTH_TO_DESTALPHA"	"0..1"
// DYNAMIC: "FLASHLIGHTSHADOWS"			"0..1"

// SKIP: ($PIXELFOGTYPE == 0) && ($WRITEWATERFOGTODESTALPHA != 0)

// We don't care about flashlight depth unless the flashlight is on
// SKIP: ( $FLASHLIGHT == 0 ) && ( $FLASHLIGHTSHADOWS == 1 )

// Flashlight shadow filter mode is irrelevant if there is no flashlight
// SKIP: ( $FLASHLIGHT == 0 ) && ( $FLASHLIGHTDEPTHFILTERMODE != 0 )

#include "common_flashlight_fxc.h"
#include "common_lightmappedgeneric_fxc.h"
#include "common_vertexlitgeneric_dx9.h"
#include "shader_constant_register_map.h"

const float4 g_DiffuseModulation			: register( PSREG_DIFFUSE_MODULATION );
const float4 g_ShadowTweaks					: register( PSREG_ENVMAP_TINT__SHADOW_TWEAKS );
const float3 cAmbientCube[6]				: register( PSREG_AMBIENT_CUBE );
const float4 g_EyePos						: register( PSREG_EYEPOS_SPEC_EXPONENT );
const float4 g_FogParams					: register( PSREG_FOG_PARAMS );
const float4 g_FlashlightAttenuationFactors	: register( PSREG_FLASHLIGHT_ATTENUATION );			// On non-flashlight pass
const float4 g_FlashlightPos				: register( PSREG_SPEC_RIM_PARAMS );
const float4x4 g_FlashlightWorldToTexture	: register( PSREG_FLASHLIGHT_TO_WORLD_TEXTURE );
PixelShaderLightInfo cLightInfo[3]			: register( PSREG_LIGHT_INFO_ARRAY );				// 2 registers each - 6 registers total (4th light spread across w's)
const float4 g_BaseColor					: register( PSREG_SELFILLUMTINT );

static const float PI = 3.141592;
static const float EPSILON = 0.00001;
static const float ENVMAPLOD = 6.0;

sampler BaseTextureSampler		: register( s0 );	// Base map, selfillum in alpha
sampler NormalTextureSampler	: register( s1 );	// Normal map
sampler EnvmapSampler			: register( s2 );	// Cubemap
sampler ShadowDepthSampler		: register( s4 );	// Flashlight shadow depth map sampler
sampler RandRotSampler			: register( s5 );	// RandomRotation sampler
sampler FlashlightSampler		: register( s6 );	// Flashlight cookie 
sampler LightmapSampler			: register( s7 );	// Lightmap
#if EMISSION
sampler EmissionTextureSampler	: register( s9 );	// Emission texture
#endif
sampler MRAOTextureSampler		: register( s10 );	// MRAO texture

struct PS_INPUT
{
	float2 baseTexCoord				: TEXCOORD0;
	float4 lightAtten				: TEXCOORD1;
	float3 worldNormal				: TEXCOORD2;
	float3 worldPos					: TEXCOORD3;
	float3 projPos					: TEXCOORD4;
	float4 lightmapTexCoord1And2	: TEXCOORD5;
	float4 lightmapTexCoord3		: TEXCOORD6;
};

// Shlick's approximation of the Fresnel factor.
float3 fresnelSchlick(float3 F0, float cosTheta)
{
	return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

// GGX/Towbridge-Reitz normal distribution function.
// Uses Disney's reparametrization of alpha = roughness^2.
float ndfGGX(float cosLh, float roughness)
{
	float alpha   = roughness * roughness;
	float alphaSq = alpha * alpha;

	float denom = (cosLh * cosLh) * (alphaSq - 1.0) + 1.0;
	return alphaSq / (PI * denom * denom);
}

float3 DirectDiffuseBRDF(float3 lightColor, float3 worldSpaceNormal, float3 worldSpaceLightDirection) {
	return (lightColor * dot(worldSpaceNormal, worldSpaceLightDirection)) / PI;
}

// Single term for separable Schlick-GGX below.
float gaSchlickG1(float cosTheta, float k)
{
	return cosTheta / (cosTheta * (1.0 - k) + k);
}

// Schlick-GGX approximation of geometric attenuation function using Smith's method.
float gaSchlickGGX(float cosLi, float cosLo, float roughness)
{
	float r = roughness + 1.0;
	float k = (r * r) / 8.0; // Epic suggests using this roughness remapping for analytic lights.
	return gaSchlickG1(cosLi, k) * gaSchlickG1(cosLo, k);
}

// Compute the matrix used to transform tangent space normals to world space
float3x3 compute_tangent_frame(float3 N, float3 P, float2 uv, out float3 T, out float3 B, out float sign_det)
{
	float3 dp1 = ddx(P);
	float3 dp2 = ddy(P);
	float2 duv1 = ddx(uv);
	float2 duv2 = ddy(uv);

	sign_det = dot(dp2, cross(N, dp1));

	float3x3 M = float3x3(dp1, dp2, cross(dp1, dp2));
	float2x3 inverseM = float2x3(cross(M[1], M[2]), cross(M[2], M[0]));
	T = normalize(mul(float2(duv1.x, duv2.x), inverseM));
	B = normalize(mul(float2(duv1.y, duv2.y), inverseM));
	return float3x3(T, B, N);
}

// Get attenuation of a light source
float GetAttenForLight(float4 lightAtten, uint lightNum)
{
#if (NUM_LIGHTS > 0)
	if (lightNum == 1) return lightAtten.y;
#endif

#if (NUM_LIGHTS > 1)
	if (lightNum == 2) return lightAtten.z;
#endif

#if (NUM_LIGHTS > 2)
	if (lightNum == 3) return lightAtten.a;
#endif

	return lightAtten.x;
}

// Calculate light for one source
float3 calculateLight(float3 lightIn, float3 lightIntensity, float3 lightOut, float3 normal, float3 fresnelReflectance, float roughness, float metalness, float lightDirectionAngle, float3 albedo)
{
	// Lh
	float3 HalfAngle = normalize(lightIn + lightOut);

	float cosLightIn = max(0.0, dot(normal, lightIn));
	float cosHalfAngle = max(0.0, dot(normal, HalfAngle));

	// F - Calculate Fresnel term for direct lighting.
	float3 F = fresnelSchlick(fresnelReflectance, max(0.0, dot(HalfAngle, lightOut)));

	// D - Calculate normal distribution for specular BRDF.
	float D = ndfGGX(cosHalfAngle, roughness);

	// Calculate geometric attenuation for specular BRDF.
	float G = gaSchlickGGX(cosLightIn, lightDirectionAngle, roughness);

	// Diffuse scattering happens due to light being refracted multiple times by a dielectric medium.
	// Metals on the other hand either reflect or absorb energso diffuse contribution is alwaysy,  zero.
	// To be energy conserving we must scale diffuse BRDF contribution based on Fresnel factor & metalness.
	float3 kd = lerp(float3(1, 1, 1) - F, float3(0, 0, 0), metalness);

	float3 diffuseBRDF = kd * albedo;

	// Cook-Torrance specular microfacet BRDF.
	float3 specularBRDF = (F * D * G) / max(EPSILON, 4.0 * cosLightIn * lightDirectionAngle);

	return (diffuseBRDF + specularBRDF) * lightIntensity * cosLightIn;
}

// Blurry cubemap sample / ambient cube, used to blend with the correct mipmap from the cubemap, to reduce artifacts
float3 specularLookup(float3 normal)
{
#if LIGHTMAPPED
	float4 specularUV = float4(normal, ENVMAPLOD);
	float3 color = ENV_MAP_SCALE * texCUBElod(EnvmapSampler, specularUV);
	return color;
#else
	float3 color = AmbientLight(normal, cAmbientCube);
	return color;
#endif
}

// Monte Carlo integration, approximate analytic version based on Dimitar Lazarov's work
// https://www.unrealengine.com/en-US/blog/physically-based-shading-on-mobile
float3 EnvBRDFApprox(float3 SpecularColor, float Roughness, float NoV)
{
	const float4 c0 = { -1, -0.0275, -0.572, 0.022 };
	const float4 c1 = { 1, 0.0425, 1.04, -0.04 };
	float4 r = Roughness * c0 + c1;
	float a004 = min( r.x * r.x, exp2( -9.28 * NoV ) ) * r.x + r.y;
	float2 AB = float2( -1.04, 1.04 ) * a004 + r.zw;
	return SpecularColor * AB.x + AB.y;
}

// Get ambient light
float3 ambientLookup(float3 normal, float3 textureNormal, float4 lightmapTexCoord1And2, float4 lightmapTexCoord3)
{
#if LIGHTMAPPED
	float2 bumpCoord1;
	float2 bumpCoord2;
	float2 bumpCoord3;
	ComputeBumpedLightmapCoordinates( lightmapTexCoord1And2, lightmapTexCoord3.xy,
			bumpCoord1, bumpCoord2, bumpCoord3 );

	float3 lightmapColor1 = LightMapSample( LightmapSampler, bumpCoord1 );
	float3 lightmapColor2 = LightMapSample( LightmapSampler, bumpCoord2 );
	float3 lightmapColor3 = LightMapSample( LightmapSampler, bumpCoord3 );

	float4 sample = tex2D( LightmapSampler, bumpCoord1 );
	
	float3 lightmapColor = textureNormal.x * lightmapColor1 +
			textureNormal.y * lightmapColor2 +
			textureNormal.z * lightmapColor3;
	lightmapColor = sample.xyz;
	return max(0, lightmapColor * LIGHT_MAP_SCALE);
#else
	// HACK
	float4 specularUV = float4(normal, ENVMAPLOD);
	float3 result = ENV_MAP_SCALE * texCUBElod(EnvmapSampler, specularUV);

	//Might be better but doesn't work reliably
	result += PI * AmbientLight(normal, cAmbientCube);
	return result;
#endif
}

// Entry point
float4 main( PS_INPUT i ) : COLOR
{

	float4 albedo = tex2D( BaseTextureSampler, i.baseTexCoord ) * g_BaseColor;
	float3 mrao = tex2D( MRAOTextureSampler, i.baseTexCoord ).xyz;
	float metalness = mrao.x;
	float roughness = mrao.y;
	float ambientOcclusion = mrao.z;

#if EMISSION
	float3 emission = tex2D( EmissionTextureSampler, i.baseTexCoord ).xyz;
#endif

	float3 surfNormal = normalize(i.worldNormal);
	float3 surfTangent;
	float3 surfBase;
	float flipSign;
	float3x3 normalBasis = compute_tangent_frame(surfNormal, i.worldPos, i.baseTexCoord, surfTangent, surfBase, flipSign);
	float3 textureNormal = (tex2D( NormalTextureSampler, i.baseTexCoord ).xyz - float3(0.5, 0.5, 0.5)) * 2;
	float3 normal = normalize(mul(textureNormal, normalBasis));

	// Fixup textureNormal for ambient lighting
	textureNormal.y *= flipSign;

	// Lo
	float3 outgoingLightDirection = normalize(g_EyePos - i.worldPos);
	// cosLo
	float lightDirectionAngle = max(0, dot(normal, outgoingLightDirection));

	// Lr
	float3 specularReflectionVector = 2.0 * lightDirectionAngle * normal - outgoingLightDirection;

	// F0
	float3 dielectricCoefficient = 0.04;
	float3 fresnelReflectance = lerp(dielectricCoefficient, albedo, metalness);

	float3 directLighting = 0.0;

	for (uint n = 0; n < NUM_LIGHTS; ++n) // now lighting works on one map but not on another??
	{
		// Li
		float3 LightIn = normalize(PixelShaderGetLightVector(i.worldPos, cLightInfo, n));
		float3 LightColor = PixelShaderGetLightColor(cLightInfo, n) * GetAttenForLight(i.lightAtten, n);

		directLighting += calculateLight(LightIn, LightColor, outgoingLightDirection,
				normal, fresnelReflectance, roughness, metalness, lightDirectionAngle, albedo);
	}

	// Start ambient
	float3 ambientLighting = 0;

	{
		float3 irradiance = ambientLookup(normal, normalize(textureNormal), i.lightmapTexCoord1And2, i.lightmapTexCoord3);

		// F
		float3 ambientLightingFresnelTerm = fresnelSchlick(fresnelReflectance, lightDirectionAngle);

		// kd
		float3 diffuseContributionFactor = 1.0 - ambientLightingFresnelTerm;

		float3 diffuseIBL = diffuseContributionFactor * albedo * irradiance;

		float4 specularUV = float4(specularReflectionVector, roughness * ENVMAPLOD);
		float3 lookup = ENV_MAP_SCALE * texCUBElod(EnvmapSampler, specularUV).xyz;
		float3 lookupBlurry = specularLookup(specularReflectionVector);

		float3 specularIrradiance = lerp(lookup, lookupBlurry, roughness * roughness); // reduces artifacts apparently

		float3 specularIBL = specularIrradiance * EnvBRDFApprox(fresnelReflectance, roughness, lightDirectionAngle);

		ambientLighting = diffuseIBL + specularIBL;
	}
	// End ambient

	
	if ( FLASHLIGHT > 0 )
	{
		float4 flashlightSpacePosition = mul( float4( i.worldPos, 1.0f ), g_FlashlightWorldToTexture );
		float3 vProjCoords = flashlightSpacePosition.xyz / flashlightSpacePosition.w;

		float3 delta = g_FlashlightPos - i.worldPos;
		float distSquared = dot( delta, delta );
		float dist = sqrt( distSquared );

		float flashlightShadow = DoFlashlightShadow(ShadowDepthSampler, RandRotSampler, vProjCoords, i.projPos, FLASHLIGHTDEPTHFILTERMODE, g_ShadowTweaks, true);
		float flashlightAttenuated = lerp( flashlightShadow, 1.0f, g_ShadowTweaks.y );	// Blend between fully attenuated and not attenuated
		float fAtten = saturate( dot( g_FlashlightAttenuationFactors.xyz, float3( 1.0f, 1.0f/dist, 1.0f/distSquared ) ) );
		flashlightShadow = saturate( lerp( flashlightAttenuated, flashlightShadow, fAtten ) );	// Blend between shadow and above, according to light attenuation
		
		
		float3 flashlightColor = tex2D( FlashlightSampler, vProjCoords );
		float farZ = g_FlashlightAttenuationFactors.w;
		float endFalloffFactor = RemapValClamped( dist, farZ, 0.6f * farZ, 0.0f, 1.0f );

		float3 flashLightIntensity = flashlightColor * flashlightShadow * endFalloffFactor;
		
		float3 flashLightIn = normalize(g_FlashlightPos.xyz - i.worldPos);

		directLighting += max(0, calculateLight(flashLightIn, flashLightIntensity, outgoingLightDirection,
				normal, fresnelReflectance, roughness, metalness, lightDirectionAngle, albedo));
	}

	float fogFactor = CalcPixelFogFactor( PIXELFOGTYPE, g_FogParams, g_EyePos.z, i.worldPos.z, i.projPos.z );

	float alpha = albedo.a;

#if WRITEWATERFOGTODESTALPHA && ( PIXELFOGTYPE == PIXEL_FOG_TYPE_HEIGHT )
	alpha = fogFactor;
#endif

	bool bWriteDepthToAlpha = ( WRITE_DEPTH_TO_DESTALPHA != 0 ) && ( WRITEWATERFOGTODESTALPHA == 0 );

	float3 combinedLighting = (directLighting + ambientLighting) * ambientOcclusion;

#if EMISSION
	combinedLighting += emission;
#endif

	return FinalOutput( float4( combinedLighting, alpha ), fogFactor, PIXELFOGTYPE, TONEMAP_SCALE_LINEAR, bWriteDepthToAlpha, i.projPos.z );	
}
